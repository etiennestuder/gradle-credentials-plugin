plugins {
  id 'com.gradle.build-scan' version '3.1.1'
  id 'nu.studer.plugindev' version '1.0.12'
  id 'nu.studer.credentials' version '2.0'
  id 'groovy'
}

group = 'nu.studer'
version = '2.1-DEV'

buildScan {
    publishAlways()
    obfuscation {
        ipAddresses { addresses -> addresses.collect { address -> '0.0.0.0' } }
    }
}

dependencies {
  compile "nu.studer:java-ordered-properties:1.0.2"
  compile "commons-codec:commons-codec:1.13"

  testCompile('org.spockframework:spock-core:1.3-groovy-2.5') {
    exclude module: 'groovy-all'
  }
}

task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files(sourceSets.main.runtimeClasspath)
        .withPropertyName('Plugin runtime classpath')
        .withNormalizer(ClasspathNormalizer)
    outputs.dir(outputDir)
        .withPropertyName('Output directory')

    doLast {
        outputDir.mkdirs()
        new File(outputDir, 'plugin-classpath.txt').text = sourceSets.main.runtimeClasspath.join('\n')
    }
}

dependencies {
    testRuntime files(createClasspathManifest)
}

plugindev {
  pluginDescription 'Gradle plugin to store and access encrypted credentials for use in Gradle builds.'
  pluginImplementationClass 'nu.studer.gradle.credentials.CredentialsPlugin'
  pluginLicenses 'Apache-2.0'
  pluginTags 'gradle', 'plugin', 'credentials'
  authorId 'etiennestuder'
  authorName 'Etienne Studer'
  authorEmail 'etienne@studer.nu'
  projectUrl 'https://github.com/etiennestuder/gradle-credentials-plugin'
  projectInceptionYear '2014'
  done()
}

bintray {
  user = credentials.BINTRAY_USER
  key = credentials.BINTRAY_API_KEY
  pkg.repo = 'gradle-plugins'
  dryRun = false
}

test {
  maxParallelForks = 2
}

def testAll = tasks.create('testAll') {
  description = 'Runs the Gradle cross-version tests.'
  group = 'Verification'
}

List<String> testedGradleVersions = []
testedGradleVersions << "6.0.1"
testedGradleVersions << "5.6.4"
testedGradleVersions << "5.0"

testedGradleVersions.each { version ->
  project.tasks.create("test_" + version.replaceAll("[^a-zA-Z0-9]", "_"), Test).with {
    systemProperty 'testContext.gradleVersion', version
    testAll.dependsOn(it)
  }
}
